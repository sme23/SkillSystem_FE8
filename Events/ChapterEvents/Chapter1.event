EventPointerTable(Chapter1EventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, ThiefTime, 3)
TurnEventPlayer(0, KhazhakWarning, 5)
TurnEventPlayer(0, KhazhakCharges, 6)
END_MAIN

ThiefTime:
SpawnReinforcements(ThiefSpawn)
FlashCursor(19, 16, 30)
FlashCursor(Lua, 30)
Text(Ch1ThiefArrival)
NoFade
ENDA

ThiefSpawn:
UNIT Blackheart Thief 0 Level(2, Enemy, True) [19,16] 0 0 0 0 [IronKnife, ChestKey] ThiefAI
UNIT

KhazhakWarning:
CameraCursor(Khazhak)
Text(Ch1KhazhakWarning)
NoFade
ENDA

KhazhakCharges:
CameraCursor(Khazhak)
Text(Ch1KhazhakCharges)
SetAI(Khazhak,0,0,4,0)
SetAI(Blackheart,0,0,4,0)
NoFade
ENDA

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
DurabilityChest(SkillScroll,CharismaID,18,11)
END_MAIN

MiscBasedEvents:
DefeatAll(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Units1:
UNIT

Traps1:
RedSigil(19,9)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
ASMC ClearAllDebuffs // now clears tonics, too. If you add a prep screen to this chapter, then restarting the chapter after saving will remove any tonic boosts because this ASMC is being called each time. 
LOAD1 1 XimoGroup
ENUN
WEATHER 7
SetBackground(Inn)
MUSC MeetTheCrewID
Text(Ch1BeginningScene1)
FadeOutMusic
CAMERA_CENTERED [3,19]
EraseBackground
LOAD1 1 RazvanGroup
ENUN
REMOVEUNIT Razvan
LOAD1 1 PilarLuaDesiGroup
ENUN
CameraCursor(16,11)
CameraCursor(Lua)
MUSC SwabbieTalkID
Text(Ch1BeginningScene2)
MOVE 15 Pilar [6,18]
MOVE 15 Ximo [6,21]
ENUN
TILECHANGE 0
MOVE 15 Desiderio [6,16]
STAL 30
SOUN 0x00B1 
MOVE 15 Desiderio [6,22]
STAL 30
SOUN 0x00B1

ENUN
STAL 30
MOVE 15 Lua [12,19]
MOVE 15 Desiderio [12,20]
ENUN
Text(Ch1BeginningScene3)
CamOn
LOAD1 1 KhazhakGroup
ENUN
CamOff
FadeOutMusic
FlashCursor(Khazhak,60)
MUSC FinInTheCloudsID
Text(Ch1BeginningScene4)
MOVE 15 Lua [6,19]
MOVE 15 Desiderio [6,20]
MOVE 15 Pilar [5,19]
ENUN
CamOn
LOAD1 1 EnemyGroupOne
ENUN
LOAD1 1 EnemyGroupTwo
ENUN
LOAD1 1 EnemyGroupThree
ENUN
CamOn
MOVE 15 Khazhak [17,8]
ENUN
CamOff
FadeOutMusic
CAMERA_CENTERED Lua
LOAD1 1 RazvanCarmenGroup
ENUN 
FlashCursor(Carmen,60)
MUSC NewHandsOnDeckID
Text(Ch1BeginningScene5)
FlashCursor(Razvan,60)
Text(Ch1BeginningScene6)
Text(Ch1BeginningScene7)
ENUN
NoFade
ENDA


XimoGroup:
UNIT Ximo Archer 0 Level(1,Ally,False) [5,19] 0 0 0 0 [IronBow, Vulnerary] NoAI
UNIT

RazvanGroup:
UNIT Razvan Curate 0 Level(4,Ally,False) [4,18] 0 0 1 REDA4R23 [Mend, Heal, Vulnerary] NoAI
UNIT

PilarLuaDesiGroup:
UNIT Lua Captain 0 Level(8,Ally,False) [4,18] 0 0 1 REDA5R20 [SteelSword, IronMusket, Vulnerary] NoAI
UNIT Desiderio Vanguard 0 Level(8,Ally,False) [3,18] 0 0 1 REDA6R19 [SteelAxe, IronLance, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(12,Ally,False) [3,18] 0 0 1 REDA4R19 [SilverKnife, SteelDagger, IronKnife, Vulnerary] NoAI
UNIT

RazvanCarmenGroup:
UNIT Razvan Curate 0 Level(4,Ally,False) [3,23] 0 0 1 REDA5R20 [Mend, Heal, Vulnerary] NoAI
UNIT Carmen Mage_F 0 Level(1,Ally,False) [4,23] 0 0 1 REDA6R18 [Fire, Vulnerary] NoAI
UNIT 

KhazhakGroup:
UNIT Khazhak Fighter 0 Level(5,Enemy,False) [17,19] 0 0 1 REDA13R19 [SteelAxe, HandAxe, HideShield] BossAI
UNIT

EnemyGroupOne:
UNIT Blackheart Mercenary 0 Level(2, Enemy, True) [18,14] 0 0 1 REDA15R14 [SteelSword] AttackInRangeAI
UNIT Blackheart Hunter 0 Level(2, Enemy, True) [13,14] 0 0 1 REDA12R15 [IronBow] AttackWhenInRangeAI
UNIT

EnemyGroupTwo:
UNIT Blackheart Deserter 0 Level(2, Enemy, True) [13,14] 0 0 1 REDA12R20 [IronLance] ChargeAI
UNIT Blackheart Fighter 0 Level(2, Enemy, True) [13,14] 0 0 1 REDA12R19 [IronAxe] ChargeAI
UNIT Blackheart Warden 0 Level(3, Enemy, True) [17,19] 0 0 1 REDA13R18 [SteelSword, Javelin] BossAI
UNIT Blackheart Hunter 0 Level(2, Enemy, True) [17,19] 0 0 1 REDA18R21 [IronBow] ChargeAI
UNIT

EnemyGroupThree:
UNIT Blackheart Deserter 0 Level(2, Enemy, True) [14,8] 0 0 1 REDA13R11 [SteelLance] AttackWhenInRangeAI
UNIT Blackheart Fighter 0 Level(2, Enemy, True) [19,9] 0 0 0 0 [IronAxe] AttackWhenInRangeAI
UNIT Blackheart Warden 0 Level(2, Enemy, True) [14,8] 0 0 1 REDA15R12 [SteelSword, Javelin] BossAI
UNIT Blackheart Warden 0 Level(2, Enemy, True) [19,9] 0 0 1 REDA16R12 [SteelLance, Javelin] BossAI
UNIT Blackheart Deserter 0 Level(2, Enemy, True) [14,8] 0 0 1 REDA17R7 [SteelLance] AttackInTwoAI
UNIT Blackheart Hunter 0 Level(2, Enemy, True) [14,8] 0 0 1 REDA18R8 [SteelBow] AttackWhenInRangeAI
UNIT



EndingScene:
FadeOutMusic
SetBackground(Ship)
MUSC ABlackheartsPrideID
Text(Ch1EndingScene1)
MUSC CherishTheDeadID
Text(Ch1EndingScene2)
Text(Ch1EndingScene3)
GoToHubChapter(HubA)
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN
