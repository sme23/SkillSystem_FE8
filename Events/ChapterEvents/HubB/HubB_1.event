EventPointerTable(HubB_1EventsID, PointerList)

#define TC_B1Cutscene1 0xB
#define TC_B1Cutscene2 0xC

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, WyvernWarning, 4)
TurnEventEnemy(0, WyvernSpawn, 5)
TurnEventPlayer(0, ThiefSpawnB, 6)
TurnEventPlayer(0, ThiefSpawnC, 7)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Chest(HexlockShield, 2, 2)
Chest(Elsurge, 5, 2)
Chest(FliersPermit, 12, 9)
Chest(SilverBow, 16, 12)
Chest(FlameSword, 0, 15)
Chest(InfiltratorGun, 2, 15)
Door(3,4)
Door(4,4)
Door(12,11)
Door(3,16)
Door(3,13)
END_MAIN

MiscBasedEvents:
DefeatBoss(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
UNIT Lua Captain 0 Level(10,Ally,False) [15,5] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [16,6] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [16,4] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [15,3] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [17,5] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [18,6] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [18,3] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [18,4] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [16,3] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [16,1] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [17,2] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT Indio Shaman 0 Level(1, Ally, False) [16,0] 0 0 0 0 [Surge, Vulnerary] NoAI
UNIT

AntlionsInitial:
//Temp
//UNIT Kiril MasterNinja 0 Level(13,Enemy,True) [9,20] 0 0 0 0 [SteelDagger, Wingclipper] BossAI
//boss squad
UNIT Kiril MasterNinja 0 Level(13,Enemy,True) [9,20] 0 0 0 0 [SteelDagger, Wingclipper] GroupOneAI
UNIT Antlion Brigand 0 Level(10, Enemy, True) [7,21] 0 0 0 0 [Crusher] GroupOneAI
UNIT Antlion Deserter 0 Level(10, Enemy, True) [10,21] 0 0 0 0 [Pike] GroupOneAI
//turn 1
UNIT Antlion Mercenary 0 Level(9, Enemy, True) [11,3] 0 0 0 0 [SteelSword] ChargeAI
UNIT Antlion Brigand 0 Level(9, Enemy, True) [13,1] 0 0 0 0 [SteelAxe] ChargeAI
UNIT Antlion Hunter 0 Level(9, Enemy, True) [12,4] 0 0 0 0 [MiniBow] ChargeAI
UNIT Antlion Mage 0 Level(9, Enemy, True) [11,5] 0 0 0 0 [Elfire] ChargeAI
UNIT Antlion Myrmidon 0 Level(9, Enemy, True) [8,0] 0 0 0 0 [Rapier] AttackWhenInRangeAI
UNIT Antlion Fighter 0 Level(9, Enemy, True) [7,8] 0 0 0 0 [SteelAxe] ChargeAI
//turn 2
//UNIT Antlion Warden 0 Level(10, Enemy, True) [8,6] 0 0 0 0 [IronBlade, Harpoon] BossAI
//UNIT Antlion Vanguard 0 Level(10, Enemy, True) [9,7] 0 0 0 0 [Harpoon, SteelAxe] BossAI
UNIT Antlion Mercenary 0 Level(9, Enemy, True) [9,12] 0 0 0 0 [IronSword] AttackWhenInRangeAI
UNIT Antlion Brigand 0 Level(9, Enemy, True) [3,6] 0 0 0 0 [SteelAxe] AttackWhenInRangeAI
//turn 3
UNIT Antlion Shaman 0 Level(9, Enemy, True) [3,9] 0 0 0 0 [Elsurge] AttackWhenInRangeAI
UNIT Antlion Deserter 0 Level(9, Enemy, True) [6,12] 0 0 0 0 [IronLance] AttackWhenInRangeAI
UNIT Antlion Vanguard 0 Level(9, Enemy, True) [2,11] 0 0 0 0 [SteelAxe] AttackInTwoAI
UNIT Antlion Hunter 0 Level(9, Enemy, True) [0,8] 0 0 0 0 [Longbow] AttackWhenInRangeAI
//turn 4
UNIT Antlion Thief 0 Level(9, Enemy, True) [6,15] 0 0 0 0 [IronDagger] AttackWhenInRangeAI
UNIT Antlion Ninja 0 Level(9, Enemy, True) [11,14] 0 0 0 0 [Stiletto] AttackWhenInRangeAI
UNIT Antlion Fighter 0 Level(9, Enemy, True) [8,16] 0 0 0 0 [SteelAxe,IronShield] AttackWhenInRangeAI
UNIT Antlion SwordCav 0 Level(9, Enemy, True) [12,16] 0 0 0 0 [SteelSword] AttackWhenInRangeAI
//turn 5
UNIT Antlion Mercenary 0 Level(9, Enemy, True) [6,18] 0 0 0 0 [SteelSword] AttackWhenInRangeAI
UNIT Antlion Myrmidon 0 Level(9, Enemy, True) [11,17] 0 0 0 0 [Lancereaver] AttackWhenInRangeAI
UNIT Antlion Shaman 0 Level(10, Enemy, True) [12,20] 0 0 0 0 [Elfire] AttackWhenInRangeAI
UNIT Antlion Warden 0 Level(10, Enemy, True) [5,19] 0 0 0 0 [SteelBlade,Horseslayer] AttackInTwoAI
UNIT Antlion Gunner 0 Level(10, Enemy, True) [16,17] 0 0 0 0 [SteelMusket,Pistol] AttackWhenInRangeAI
UNIT Antlion Mercenary 0 Level(10, Enemy, True) [17,19] 0 0 0 0 [SteelSword] AttackWhenInRangeAI
//nw chest room
UNIT Antlion Soldier 0 Level(9, Enemy, True) [2,1] 0 0 0 0 [SteelLance] ChargeAI
UNIT Antlion Mercenary 0 Level(9, Enemy, True) [5,1] 0 0 0 0 [SteelSword] ChargeAI
//sw chest room
UNIT Antlion Soldier 0 Level(9, Enemy, True) [0,15] 0 0 0 0 [SteelLance] ChargeAI
UNIT Antlion Brigand 0 Level(9, Enemy, True) [1,15] 0 0 0 0 [Swordreaver] ChargeAI
//se chest room
UNIT Antlion Warden 0 Level(10, Enemy, True) [16,11] 0 0 0 0 [SteelSword] AttackWhenInRangeAI
UNIT Antlion Vanguard 0 Level(10, Enemy, True) [14,9] 0 0 0 0 [Oversize] AttackWhenInRangeAI
UNIT

WyvernReinforcements:
UNIT Antlion MaligKnight 0 Level(11, Enemy, True) [18,3] 0 0 1 REDA15R3 [SteelAxe, Elfire] ChargeAI
UNIT Antlion DragonRider 0 Level(10, Enemy, True) [18,2] 0 0 1 REDA16R2 [SteelAxe, HandAxe] ChargeAI
UNIT Antlion DragonRider_F 0 Level(10, Enemy, True) [18,4] 0 0 1 REDA16R4 [SteelAxe, Hammer] ChargeAI
UNIT


ThiefA:
UNIT Antlion Thief 0 Level(9, Enemy, True) [1,3] 0 0 0 0 [IronKnife] ThiefAI
UNIT

ThiefB:
UNIT Antlion Thief 0 Level(9, Enemy, True) [18,13] 0 0 0 0 [IronKnife, ChestKey] ThiefAI
UNIT

ThiefC:
UNIT Antlion Thief 0 Level(9, Enemy, True) [0,8] 0 0 0 0 [IronKnife,ChestKey,DoorKey] ThiefAI
UNIT

DmitrSpawn:
UNIT Dmitr Brigand 0 Level(9,Ally,True) [1,1] 0 0 0 0 [Bloodaxe] NoAI
UNIT

IntroCutsceneNPCs:
UNIT GreagorGuard Hunter 0 Level(3,NPC, False) [2, 6] 0 0 0 0 [] NoAI
UNIT GreagorPatron VillagerMAdult 0 Level(1,NPC,False) [11,1] 0 0 0 0 [] NoAI
UNIT GreagorPatron VillagerFAdult 0 Level(1,NPC,False) [7,0] 0 0 0 0 [] NoAI
UNIT GreagorGuard Deserter 0 Level(3,NPC, False) [10, 6] 0 0 0 0 [] NoAI
UNIT

//1st cutscene

IntroCutsceneLuaRaz:
UNIT Lua Captain 0 Level(10,Ally,False) [2,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [8,3] 0x0 0x0 0 0 [0x0] NoAI
UNIT

//pilar appears shortly after lua
IntroCutscenePilar:
UNIT Pilar 0 0 Level(1,Ally,True) [2,2] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneDmitr:
UNIT Dmitr Brigand 0 Level(9,NPC,True) [8,3] 0 0 0 0 [Bloodaxe] NoAI
UNIT

IntroCutsceneDesiderio:
UNIT Desiderio 0 0 Level(4,Ally,True) [6,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT


//2nd part of cutscene

IntroCutsceneDesertCrew1:
UNIT Lua Captain 0 Level(10,Ally,False) [9,0] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Dmitr Brigand 0 Level(9,NPC,True) [10,0] 0 0 0 0 [Bloodaxe] NoAI
UNIT

IntroCutsceneDesertCrew2:
UNIT Pilar 0 0 Level(1,Ally,True) [10,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT Razvan 0 0 Level(1,Ally,True) [9,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT Desiderio Cataphract 0 Level(4,Ally,True) [8,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT

IntroCutsceneAntlionScout:
UNIT Antlion MaligKnight 0 Level(4,Enemy,True) [0,4] 0x0 0x0 0 0 [0x0] NoAI
UNIT 

//3rd part of cutscene

UNIT:
IntroCutsceneKiril:
UNIT Kiril MasterNinja 0 Level(13,Enemy,True) [13,4] 0 0 0 0 [SteelDagger, Wingclipper] BossAI
UNIT Antlion Ninja 0 Level(13,Enemy,True) [12,5] 0 0 0 0 [SteelDagger, Wingclipper] BossAI
UNIT

IntroCutsceneAntlionAlerted:
UNIT GreagorGuard WyvernRider 0 Level(4,Enemy,True) [18,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT GreagorPatron MaligKnight 0 Level(4,Enemy,True) [17,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT GreagorLibrarian WyvernRider 0 Level(4,Enemy,True) [16,0] 0x0 0x0 0 0 [0x0] NoAI
UNIT

CutsceneUnits:
UNIT Lua Captain 0 Level(10,Ally,False) [18,0] 0 0 1 REDA16R4 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Dmitr Brigand 0 Level(10,NPC,False) [17,0] 0 0 1 REDA15R3 [Bloodaxe, RecoilAxe, Vulnerary] NoAI
UNIT Desiderio Captain 0 Level(10,Ally,False) [16,0] 0 0 1 REDA15R5 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

BeginningScene:

	//The Skysabers are in a small village near the destination they're looking for.
	FADI 60
	LoadCutsceneUnits(IntroCutsceneNPCs)
	TILECHANGE TC_B1Cutscene1
	MUSC MeetTheCrewID
	CAMERA_CENTERED [0,0]
	FADU 16
	BROWNBOX HubB_1TownName [0,0]
	STAL 60
	REMA
	CameraCursor(2,2)
	SetBackground(Village)
	Text(HubB_1BeginningScene1)
	EraseBackground
	STAL 20
	FlashCursor(8,3, 60)
	SetBackground(Village)
	Text(HubB_1BeginningScene2)
	EraseBackground
	
	//The crew fail to get any good information, and gather outside. 
	MUSC StillZephyrsID
	STAL 20
	LoadCutsceneUnits(IntroCutsceneLuaRaz)
	MOVE 5 Lua [2,4]
	MOVE 5 Razvan [4,3] 
	STAL 15
	LoadCutsceneUnits(IntroCutscenePilar)
	MOVE 5 Pilar [2,3]
	ENUN
	CameraCursor(Lua)
	SetBackground(Outside_Village_Evening)
	CHECK_EVENTID WentToHub2
	BNE 775 0xC 0x0 //if hub 2 completed, play slightly altered dialogue.
		Text(HubB_1BeginningScene3_B2NotCompleted)
		GOTO 776
	LABEL 775
		Text(HubB_1BeginningScene3_B2Completed)
	LABEL 776
	EraseBackground
	
	//Dmitr comes in, inquires about the ruins. 
	LoadCutsceneUnits(IntroCutsceneDmitr)
	MOVE 5 Dmitr [5,4]
	ENUN
	CameraCursor(Dmitr)
	SetBackground(Outside_Village_Evening)
	Text(HubB_1BeginningScene4)
	EraseBackground
	
	//Desiderio arrives after looking for supplies, he's a little late, but it's so whole crew can be there
	LoadCutsceneUnits(IntroCutsceneDesiderio)
	MOVE 4 Desiderio [3,5]
	ENUN
	CameraCursor(Desiderio)
	SetBackground(Outside_Village_Evening)
	Text(HubB_1BeginningScene5)
	EraseBackground
	
	//Clean-up and SFX before next scene
	STAL 20
	FADI 10
	MUSC 0x7FFF
	MOVE 5 Lua [3,9]
	MOVE 5 Razvan [4,9] 
	MOVE 5 Pilar [5,9]
	MOVE 5 Desiderio [5,9]
	ENUN
	CLEA 0
	CLEN 0
	CLEE 0
	ENUN
	
	//Crew arrive at the desert. 
	//"Ah, the desert...count how many grains of sand there are, Razvan."
	TILEREVERT TC_B1Cutscene1
	MUSC ToTheSkiesID
	TILECHANGE TC_B1Cutscene2
	CAMERA_CENTERED [8,0]
	LoadCutsceneUnits(IntroCutsceneAntlionScout)
	LoadCutsceneUnits(IntroCutsceneDesertCrew1)
	FADU 16
	MOVE 5 Lua [12,5]
	MOVE 5 Dmitr [11,6]
	ENUN
	LoadCutsceneUnits(IntroCutsceneDesertCrew2)
	MOVE 4 Desiderio [9,4]
	MOVE 5 Razvan [10,3] 
	MOVE 5 Pilar [11,4]
	ENUN
	//Now in the desert, Lua and Dmitr discuss briefly on how much further they need to go. 
	FlashCursor(Lua, 60)
	SetBackground(Desert)
	Text(HubB_1BeginningScene6)
	EraseBackground
	
	MOVE 5 Lua [12,12]
	MOVE 5 Dmitr [13,12]
	ENUN
	MOVE 3 Desiderio [11,8]
	MOVE 4 Razvan [12,7] 
	MOVE 4 Pilar [13,8]
	ENUN
	FlashCursor(Desiderio,60)
	SetBackground(Desert)
	//Cue a small conversation about Desiderio walking through the 
	//desert, with different dialogue if promoted into a cataphract.
	SVAL s1 Cataphract
	CHECK_CLASS Desiderio
	BNE 778 0xC 0x1 //If Desiderio promoted into a Cataphract, play slightly different dialogue. 
		Text(HubB_1BeginningScene7_DesiMounted)
		GOTO 779
	LABEL 778
		Text(HubB_1BeginningScene7_DesiNotMounted)
	LABEL 779
	EraseBackground
	
	//Rest of the crew moves out, not wanting to fall behind.
	MUSC 0x7FFF
	MOVE 4 Desiderio [11,13]
	MOVE 4 Razvan [12,13]
	MOVE 4 Pilar [13,13]
	ENUN
	STAL 60

	//Oh-no, a wyvern was stalking them the whole time!
	CAMERA_CENTERED [0,0]
	STAL 30
	MUSC SwabbieTalkID
	CameraCursor(0,4)
	Text(HubB_1BeginningScene8)
	MOVE 10 Antlion [1,11]
	STAL 15
	FADI 16
	ENUN
	CLEA 0
	CLEN 0
	CLEE 0
	TILEREVERT TC_B1Cutscene2
	CLEAN
	
	//Wyvern returns to base in-tow with other wyverns, reporting in the info.
	CAMERA_CENTERED [13,0]
	LoadCutsceneUnits(IntroCutsceneKiril)
	FADU 12
	LoadCutsceneUnits(IntroCutsceneAntlionAlerted)
	MOVE 7 GreagorPatron [13,2]
	MOVE 7 GreagorLibrarian [14,2] 
	MOVE 7 GreagorGuard [12,2]
	ENUN
	FlashCursor(Kiril, 60)
	Text(HubB_1BeginningScene9)
	MOVE 7 GreagorPatron [18,4]
	MOVE 7 GreagorLibrarian [18,3] 
	MOVE 7 GreagorGuard [18,2]
	ENUN
	REMOVEUNIT GreagorPatron
	REMOVEUNIT GreagorLibrarian
	REMOVEUNIT GreagorGuard
	STAL 50
	Text(HubB_1BeginningScene10)
	FADI 12
	ENUN
	CLEE 0
	
	//Map reloads, with enemies deployed. 
	LOAD1 1 AntlionsInitial
	ENUN
	FADU 5
	CamOn
	//CAMERA_CENTERED[14,3]
	LOAD1 1 CutsceneUnits
	ENUN
	CamOff
	CAMERA Lua
	MUSC AvastMeHeartiesID
	Text(HubB_1BeginningScene11)
	CUSA Dmitr
	STAL 40
	FADI 10
	LOAD1 1 DmitrSpawn
	REMOVEALL_BLUE
	GoToPrepScreen2
	CAMERA_CENTERED[14,4]
	FADU 10
	NoFade
	//CALL EndingScene
	ENDA

	/*LOAD1 1 DmitrSpawn
	LOAD1 1 AntlionsInitial
	Text(HubB_1EndingScene1_B)
	GotoPrepScreen
	NoFade
	ENDA*/


EndingScene:
	FADI 5
	RemoveAll
	SetBackground(Fort_2)
	CHECK_ALIVE Dmitr
	BNE 1 0xC 0x0 //If Dmitr does not exist continue, otherwise jump to 1
		Text(HubB_1EndingScene1_B)
		GOTO 2
	LABEL 1
	Text(HubB_1EndingScene1)
	LABEL 2
	GoToHubChapter(HubB)
	ENDA
	
WyvernWarning:
	MUSI
	STAL 20
	CAMERA_CENTERED [15,2]
	STAL 30
	SOUN 0x263
	STAL 15
	SOUN 0x263
	STAL 15
	SOUN 0x263
	STAL 15
	SOUN 0x264
	STAL 40
	CHECK_DEPLOYED Pilar
	BEQ 1 0xC 0x0 //If Pilar isn't deployed, check Dmitr to warn.
		CameraCursor(Pilar)
		Text(HubB_1WyvernWarning_Pilar)
		Text(HubB_1WyvernWarning_LuaResponse)
		GOTO 776
	LABEL 1
	CHECK_ALIVE Dmitr
	BEQ 2 0xC 0x0 //If Dmitr also isn't alive, then fallback to Lua. 
		CameraCursor(Dmitr)
		Text(HubB_1WyvernWarning_Dmitr)
		Text(HubB_1WyvernWarning_LuaResponse)
		GOTO 776
	LABEL 2
	//Else, have Lua warn about the wyverns.
	CameraCursor(Lua)
	Text(HubB_1WyvernWarning_LuaSolo)
	LABEL 776
	MUNO
	NoFade
	ENDA

WyvernSpawn:
	SpawnReinforcements(WyvernReinforcements)
	Text(HubB_1WyvernsAppear)
	NoFade
	ENDA

ThiefSpawnB:
	SpawnReinforcements(ThiefB)
	Text(ThiefTextB_1)
	NoFade
	ENDA

ThiefSpawnC:
	SpawnReinforcements(ThiefC)
	NoFade
	ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN