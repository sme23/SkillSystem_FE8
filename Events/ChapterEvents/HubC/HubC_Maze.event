EventPointerTable(HubC_MazeEventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN

CharacterBasedEvents:
CharacterEvent(HubC_Windy_Recruited_Flag, Windy_Recruitment_Convo, Lua, Windy)
CharacterEvent(0, Windy_Friend_Convo, Lua, KeeperC2)
END_MAIN

Windy_Friend_Convo:
CHECK_EVENTID HubC_Windy_Recruited_Flag
BEQ 1 s0 sC //if not recruited, then we go to the second text entry which is basically important info
    Text(HubCWindyFriendPostRecruitmentText)
LABEL 1
Text(HubCWindyFriendPreRecruitmentText)
LABEL 2
NoFade
ENDA

Windy_Recruitment_Convo: //here, let's just have windy force herself into your party, somehow catching Lua by storm
MUSS WelcomeAboardMateyID
Text(HubCWindyFoundInTheMazeText)
FlashCursor(Windy, 60)
CUSA Windy
CamOn
MOVE 20 Windy [0,12]
ENUN
FADI 10
MOVE 15 Lua [7,2]
SVAL s1 0x9
SET_CONDITION Windy
FADU 10
MURE 2
CameraCursor(Lua)
Text(HubCLuaTakesShortcutText)
FlashCursor(6,2,30)
TILECHANGE 5
NoFade
ENDA

LocationBasedEvents:
END_MAIN

MiscBasedEvents:
AREA 0 GoBackToHubC [0,10] [0,14] //no flag required
AFEV 0 RefreshLua 0
END_MAIN

GoBackToHubC:
FADI 15
SET_ACTIVE 0
STAL 30
RemoveAll
LOMA HubC
ENUN
CALL SpawnHubCUnits
ENUN
SET_ACTIVE Lua
CAMERA_CENTERED Lua
ENUN
FOGVISION 0
ENUT InAHubFlag
FADU 15
NoFade
ENDA

RefreshLua:
SVAL s1 0x42
UNSET_CONDITION Lua
NoFade
ENDA

Traps1:
BlueSigil(0,10)
BlueSigil(0,11)
BlueSigil(0,12)
BlueSigil(0,13)
BlueSigil(0,14)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
UNIT Lua Captain 0 Level(10,Ally,False) [12,13] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

BeginningScene: //doesn't matter, will never play
NoFade
ENDA

EndingScene: // same
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN