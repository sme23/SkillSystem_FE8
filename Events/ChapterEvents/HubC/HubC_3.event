
EventPointerTable(HubC_3EventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,C3FirstTurnEvent,1)
TurnEventPlayer(0,Turn4Reinforcements,4)
TurnEventPlayer(0,RimaJoins,5)
TurnEventNPC(0,Turn7SalmanWakesUp,7)
TurnEventPlayer(0,Turn8Reinforcements,8)
TurnEventPlayer(0,Turn9ReinforcementWarning,9)
TurnEventPlayer(0,Turn10Reinforcements,10)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Escape(0,19)
Escape(0,20)
Escape(0,21)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Traps1:

//Player/Regular NPC escape points
BlueSigil(0,19)
BlueSigil(0,20)
BlueSigil(0,21)

//Traitor NPC escape points
RedSigil(25,14)
RedSigil(25,15)
RedSigil(25,16)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

ALIGN 4

Units1:
UNIT Lua Captain 0 Level(10,Ally,False) [3,7] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [2,7] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [5,7] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

UNIT Pilar Ninja 0 Level(10,Ally,False) [1,6] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [4,6] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

UNIT Pilar Ninja 0 Level(10,Ally,False) [0,5] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [2,5] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [4,5] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

UNIT Pilar Ninja 0 Level(10,Ally,False) [0,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [2,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [3,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [5,4] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

UNIT Pilar Ninja 0 Level(10,Ally,False) [4,3] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [5,2] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI

UNIT

NewUnit_Bolton:
UNIT Bolton DarkKnight 0 Level(18,Ally,False) [1,21] 0 0 0 0 [HeroFlare,Elfire,Sidepierce,RustyHook] NoAI
UNIT

NewUnit_RimaOffscreen:
UNIT Rima AxeCav 0 Level(15,Ally,False) [255,1] 0 0 0 0 [RecoilAxe,SlimAxe,HandAxe,Vulnerary] NoAI
UNIT

NewUnit_Rima:
UNIT Rima AxeCav 0 Level(15,Ally,False) [0,20] 0 0 1 REDA4R21 [RecoilAxe,SlimAxe,HandAxe,Vulnerary] NoAI
UNIT

EnemyUnits:

//Middle/Traitor escape point
UNIT Jasper AxeKnight 0 Level(20,Enemy,True) [22,13] 0 0 0 0 [SilverAxe,Crusher] AttackInRangeAI
UNIT C3Pirate Halberdier 0 Level(18,Enemy,True) [17,14] 0 0 0 0 [SilverLance] AttackInTwoAI
UNIT C3Pirate Ninja 0 Level(16,Enemy,True) [20,15] 0 0 0 0 [SteelKnife] AttackInRangeAI
UNIT C3Pirate WyvernRider 0 Level(16,Enemy,True) [13,16] 0 0 0 0 [Hammer] AttackInRangeAI
UNIT C3Pirate GriffinRider 0 Level(16,Enemy,True) [11,19] 0 0 0 0 [SteelLance] AttackInRangeAI
UNIT C3Pirate Mage 0 Level(16,Enemy,True) [17,19] 0 0 0 0 [Elfire,TealGem] AttackInRangeAI
UNIT C3Pirate Bombardier 0 Level(18,Enemy,True) [21,20] 0 0 0 0 [SilverLance,Knifekiller] AttackInRangeAI

//North Wave 1
UNIT C3Pirate Vanguard 0 Level(16,Enemy,True) [1,9] 0 0 0 0 [SteelAxe] NoAI
UNIT C3Pirate Archer 0 Level(15,Enemy,True) [0,10] 0 0 0 0 [SteelBow] NoAI
UNIT C3Pirate Hero 0 Level(18,Enemy,True) [2,10] 0 0 0 0 [SilverSword] NoAI
UNIT C3Pirate Ninja 0 Level(16,Enemy,True) [4,11] 0 0 0 0 [SteelKnife] NoAI

//North/ Wave 1 & 2
UNIT C3Pirate Mercenary 0 Level(16,Enemy,True) [12,0] 0 0 0 0 [IronBlade] NoAI
UNIT C3Pirate Mercenary 0 Level(16,Enemy,True) [16,2] 0 0 0 0 [IronBlade] NoAI
UNIT C3Pirate Ninja 0 Level(16,Enemy,True) [17,1] 0 0 0 0 [SteelKnife] NoAI
UNIT C3Pirate Shaman 0 Level(16,Enemy,True) [8,1] 0 0 0 0 [Elfire] NoAI
UNIT C3Pirate Vanguard 0 Level(16,Enemy,True) [8,3] 0 0 0 0 [SteelLance] NoAI
UNIT C3Pirate Archer 0 Level(15,Enemy,True) [11,3] 0 0 0 0 [SteelBow] NoAI
UNIT C3Pirate Fighter 0 Level(16,Enemy,True) [7,5] 0 0 0 0 [Hammer] NoAI

//Northeast
UNIT C3Pirate Adventurer 0 Level(18,Enemy,True) [21,9] 0 0 0 0 [SteelBow,RushStaff] AttackInRangeAI
UNIT C3Pirate Archer 0 Level(15,Enemy,True) [16,9] 0 0 0 0 [SteelBow] AttackInRangeAI
UNIT C3Pirate Halberdier 0 Level(18,Enemy,True) [21,7] 0 0 0 0 [ShortSpear] BossAI
UNIT C3Pirate Marshall 0 Level(18,Enemy,True) [20,6] 0 0 0 0 [HandAxe,Oversize] BossAI
UNIT C3Pirate Fighter 0 Level(16,Enemy,True) [19,6] 0 0 0 0 [SteelAxe] AttackInTwoAI
UNIT C3Pirate GriffinRider 0 Level(16,Enemy,True) [17,5] 0 0 0 0 [SteelLance] AttackInRangeAI
UNIT C3Pirate WyvernRider 0 Level(16,Enemy,True) [14,7] 0 0 0 0 [SteelAxe] NoAI

UNIT C3Pirate Hero 0 Level(18,Enemy,True) [23,1] 0 0 0 0 [IronBlade,WildAxe] AttackInRangeAI
UNIT C3Pirate Fighter 0 Level(16,Enemy,True) [22,3] 0 0 0 0 [SteelAxe] NoAI
UNIT C3Pirate Mercenary 0 Level(16,Enemy,True) [24,4] 0 0 0 0 [IronBlade] AttackInRangeAI

//Escape point/Wave 2
UNIT C3Pirate GriffinRider 0 Level(16,Enemy,True) [0,16] 0 0 0 0 [SteelLance] NoAI
UNIT C3Pirate Bombardier 0 Level(18,Enemy,True) [1,18] 0 0 0 0 [SteelLance,SteelBow] AttackInRangeAI
UNIT C3Pirate Fighter 0 Level(16,Enemy,True) [3,17] 0 0 0 0 [Hammer] NoAI
UNIT C3Pirate Mage 0 Level(16,Enemy,True) [6,14] 0 0 0 0 [Elfire] NoAI
UNIT C3Pirate Hero 0 Level(18,Enemy,True) [3,21] 0 0 0 0 [SilverSword] AttackInRangeAI
UNIT C3Pirate Sage 0 Level(18,Enemy,True) [0,22] 0 0 0 0 [Elfire] AttackInRangeAI

//Bridge
UNIT C3Pirate Halberdier 0 Level(18,Enemy,True) [8,21] 0 0 0 0 [ShortSpear,PureWater] BossAI
UNIT C3Pirate Marshall 0 Level(18,Enemy,True) [8,22] 0 0 0 0 [HandAxe] BossAI
UNIT C3Pirate Adventurer 0 Level(18,Enemy,True) [9,22] 0 0 0 0 [Greatbow,AssistStaff] BossAI
UNIT C3Pirate Mercenary 0 Level(16,Enemy,True) [5,23] 0 0 0 0 [IronBlade] NoAI
UNIT C3Pirate Gunner 0 Level(16,Enemy,True) [5,24] 0 0 0 0 [SteelMusket,GlassShield] NoAI
UNIT C3Pirate Deserter 0 Level(16,Enemy,True) [13,24] 0 0 0 0 [SteelLance] AttackInRangeAI

//South Grave
UNIT C3Pirate Ninja 0 Level(16,Enemy,True) [14,30] 0 0 0 0 [SteelKnife] AttackInTwoAI
UNIT C3Pirate WyvernRider 0 Level(16,Enemy,True) [20,30] 0 0 0 0 [Hammer] AttackInTwoAI
UNIT C3Pirate Marshall 0 Level(18,Enemy,True) [3,29] 0 0 0 0 [Spear,RiderBadge] AttackInRangeAI
UNIT C3Pirate Ninja 0 Level(16,Enemy,True) [1,30] 0 0 0 0 [Stiletto] NoAI
UNIT C3Pirate WyvernRider 0 Level(16,Enemy,True) [7,30] 0 0 0 0 [Hammer] NoAI

//End
UNIT

NPCUnits:
UNIT Boru Runeshield 0 Level(13,NPC,True) [4,7] 0 0 0 0 [Arcfire] EscapeOnlyAI
UNIT 0x55 Marshall 0 Level(12,NPC,True) [4,2] 0 0 0 0 [HandAxe] EscapeOnlyAI
UNIT 0x55 Warden 0 Level(10,NPC,True) [3,5] 0 0 0 0 [Peacekeeper] EscapeOnlyAI
UNIT 0x55 Vanguard 0 Level(10,NPC,True) [5,5] 0 0 0 0 [Javelin] EscapeOnlyAI

UNIT C3Traitor Ninja 0 Level(10,NPC,True) [4,6] 0 0 0 0 [0] EscapeOnlyAI
UNIT C3Traitor Deserter 0 Level(8,NPC,True) [4,1] 0 0 0 0 [0] EscapeOnlyAI
UNIT C3Traitor Hunter 0 Level(8,NPC,True) [3,4] 0 0 0 0 [0] EscapeOnlyAI
UNIT C3Traitor Mage 0 Level(8,NPC,True) [5,4] 0 0 0 0 [0] EscapeOnlyAI

UNIT Salman Swordmaster 0 Level(15,NPC,True) [24,8] 0 0 0 0 [SteelSword] EscapeOnlyAI

UNIT

Turn4ReinforcementsNEGroup:
UNIT C3Pirate WyvernRider 0 Level(16,Enemy,True) [25,3] 0 0 1 REDA25R3 [SteelAxe] NoAI
UNIT

Turn4ReinforcementsSWGroup:
UNIT C3Pirate Deserter 0 Level(16,Enemy,True) [4,30] 0 0 1 REDA4R30 [SteelLance] NoAI
UNIT

Turn8ReinforcementsSEGroup:
UNIT C3Pirate Fighter 0 Level(16,Enemy,True) [25,21] 0 0 1 REDA25R21 [SteelAxe] NoAI
UNIT C3Pirate Gunner 0 Level(16,Enemy,True) [25,22] 0 0 1 REDA25R22 [SteelMusket] NoAI
UNIT C3Pirate Bombardier 0 Level(18,Enemy,True) [25,25] 0 0 1 REDA22R25 [SteelLance,SteelBow] NoAI
UNIT

Turn10ReinforcementsSEGroup:
UNIT Runamel Hero 0 Level(25,Enemy,True) [25,21] 0 0 1 REDA25R21 [Tomahawk] NoAI
UNIT C3Pirate Halberdier 0 Level(18,Enemy,True) [25,21] 0 0 1 REDA23R21 [Spear] NoAI
UNIT C3Pirate Halberdier 0 Level(18,Enemy,True) [25,22] 0 0 1 REDA24R22 [Spear] NoAI
UNIT

//Cutscene units beyond this point
IntroGroup1:
UNIT C3Pirate DarkKnight 0 Level(18,NPC,False) [6,0] 0 0 0 0 [HeroFlare,Elfire,Sidepierce] NoAI
UNIT Rima AxeCav 0 Level(15,NPC,False) [5,0] 0 0 0 0 [RecoilAxe,SlimAxe,HandAxe,Vulnerary] NoAI
UNIT 0x55 Marshall 0 Level(12,NPC,True) [8,6] 0 0 0 0 [HandAxe] EscapeOnlyAI
UNIT 0x55 Warden 0 Level(10,NPC,True) [7,5] 0 0 0 0 [Peacekeeper] EscapeOnlyAI
UNIT 0x55 Vanguard 0 Level(10,NPC,True) [7,7] 0 0 0 0 [Javelin] EscapeOnlyAI
UNIT

IntroGroup2:
UNIT Boru Runeshield 0 Level(13,NPC,True) [0,6] 0 0 1 REDA5R6 [Arcfire] EscapeOnlyAI
UNIT

IntroGroup3:
UNIT Lua Captain 0 Level(10,Ally,False) [0,6] 0 0 1 REDA4R6 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,7] 0 0 1 REDA3R7 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,8] 0 0 1 REDA0R8 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,5] 0 0 1 REDA3R5 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,6] 0 0 1 REDA2R6 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,8] 0 0 1 REDA2R8 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,7] 0 0 1 REDA1R7 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,6] 0 0 1 REDA1R6 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,5] 0 0 1 REDA0R5 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Pilar Ninja 0 Level(10,Ally,False) [0,5] 0 0 1 REDA1R4 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT

IntroGroup4:
UNIT C3Pirate Vanguard 0 Level(16,Enemy,True) [2,7] 0 0 0 0 [SteelAxe] NoAI
UNIT C3Pirate Archer 0 Level(15,Enemy,True) [1,9] 0 0 0 0 [SteelBow] NoAI
UNIT C3Pirate Hero 0 Level(18,Enemy,True) [3,8] 0 0 0 0 [SilverSword] NoAI
UNIT C3Pirate Ninja 0 Level(16,Enemy,True) [5,9] 0 0 0 0 [SteelKnife] NoAI
UNIT C3Pirate Mercenary 0 Level(16,Enemy,True) [12,0] 0 0 0 0 [IronBlade] NoAI
UNIT C3Pirate Shaman 0 Level(16,Enemy,True) [8,1] 0 0 0 0 [Elfire] NoAI
UNIT C3Pirate Vanguard 0 Level(16,Enemy,True) [8,3] 0 0 0 0 [SteelLance,RustyHook] NoAI
UNIT C3Pirate Archer 0 Level(15,Enemy,True) [11,3] 0 0 0 0 [SteelBow] NoAI
UNIT C3Pirate Fighter 0 Level(16,Enemy,True) [7,5] 0 0 0 0 [Hammer] NoAI
UNIT C3Pirate WyvernRider 0 Level(16,Enemy,True) [14,7] 0 0 0 0 [SteelAxe] NoAI
UNIT

IntroGroup5:
UNIT Lua Captain 0 Level(10,Ally,False) [5,0] 0 0 1 REDA4R3 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Boru Runeshield 0 Level(13,NPC,True) [5,0] 0 0 1 REDA5R3 [Arcfire] EscapeOnlyAI
UNIT C3Pirate DarkKnight 0 Level(18,NPC,False) [5,0] 0 0 1 REDA5R2 [HeroFlare,Elfire,Sidepierce] NoAI
UNIT Rima AxeCav 0 Level(15,NPC,False) [5,0] 0 0 1 REDA6R2 [RecoilAxe,SlimAxe,HandAxe,Vulnerary] NoAI
UNIT

EndingGroup1:
UNIT Lua Captain 0 Level(10,Ally,False) [2,20] 0 0 0  0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Runamel Hero 0 Level(25,Enemy,False) [9,21] 0 0 0 0 [SilverAxe, SilverSword] NoAI
UNIT C3Pirate Halberdier 0 Level(18,Enemy,True) [4,24] 0 0 0 0 [Spear] NoAI
UNIT C3Traitor Halberdier 0 Level(18,Enemy,True) [6,19] 0 0 0 0 [Spear] NoAI
UNIT

EndingGroup2:
UNIT GreagorVillager MaligKnight 0 Level(20,NPC,False) [0,17] 0 0 1 REDA3R20 [0] NoAI
UNIT


//----------------------------------------------
//BeginningScene
//----------------------------------------------

BeginningScene:
//Objective progress is tracked here
COUNTER_SET 0 0
COUNTER_SET 1 0

//During this scene, the first several units in the player's party are loaded. If player has saved in preps, temporarily hide Bolton (NPC Dark Knight uses a different unit ID)
CHECK_EXISTS Bolton
IFNE 1 sC s0
	REMU Bolton
LABEL 1

WEATHER 7
LoadCutsceneUnits(IntroGroup1)
TILECHANGE 0
ClearBackground
STAL 45
LOAD1 0 IntroGroup2
LOAD3 2 IntroGroup3 //this is what Bolton is hidden for
ENUN
STAL 15
FlashCursor(Lua, 40)
FlashCursor(Boru, 40)
STAL 15
MUSC ToTheSkiesID
Text(HubC_3Intro1)
STAL 20

MOVE 0 Rima [5,4]
MOVE 0 C3Pirate [6,4]
ENUN
STAL 15
Text(HubC_3Intro2)
STAL 20

MOVE 0 Rima [0,6]
MOVE 0 C3Pirate [0,7]
ENUN
DISA Rima
DISA C3Pirate
STAL 10
TEXTSTART
TEXTSHOW(HubC_3Intro3)
TEXTEND
FADI 10
REMA
STAL 45

TILECHANGE 1
CLEA 0
CLEN 0
CHECK_EXISTS Bolton
IFNE 2 sC s0
	REVEAL Bolton
LABEL 2
SetBackground(0x28) //inside of ruined fort
MUSC SwabbieTalkID
TEXTSTART
TEXTSHOW(HubC_3Flashback1)
TEXTEND
FADI 10
REMA
STAL 20

SetBackground(0x00) //inside of house
TEXTSTART
TEXTSHOW(HubC_3Flashback2)
TEXTEND
FADI 10
REMA
STAL 45

SetBackground(0x1D) //wilderness, close to mountains
MUSC WindAmbienceID
TEXTSTART
TEXTSHOW(HubC_3Intro4)
TEXTEND
FADI 10
REMA
STAL 20

SetBackground(0x1B) //wilderness, mountains in the distance
MUSC AvastMeHeartiesID
TEXTSTART
TEXTSHOW(HubC_3Intro5)
TEXTEND
FADI 10
REMA
LoadCutsceneUnits(IntroGroup4)
STAL 20

MUSI
SetBackground(0x28) //inside of ruined fort
TEXTSTART
TEXTSHOW(HubC_3Intro6)
TEXTEND
FADI 10
REMA

ClearBackground
STAL 15
LoadCutsceneUnits(IntroGroup5)
STAL 20
MUNO
Text(HubC_3Intro7)
STAL 15
MOVE 0 Rima [5,16]
ENUN
DISA Rima
Text(HubC_3Intro8)
STAL 10

CenterTextbox(HubC_3Explanation)
STAL 10
CenterTextbox(HubC_3Explanation2)

//Done with cutscenes; setup for gameplay
FADI 10
CLEN 0
CLEE 0
LoadEnemies(EnemyUnits)
LoadPlayerUnits(NewUnit_Bolton)
LOAD1 1 NPCUnits
ENUN
SetUnitStatusAll(Salman,BadStatus,0x72,AnyDeployed,AnyArmy,AnyClass,Area(24,8,24,8),AnyItem) //7 turns of Sleep on Salman
Rescue(4,7,4,6)
Rescue(4,2,4,1)
Rescue(3,5,3,4)
Rescue(5,5,5,4)

GotoPrepScreen
NoFade
ENDA


//----------------------------------------------
//Turn Events
//----------------------------------------------

C3FirstTurnEvent:
CenterTextbox(EarlyLeaveExplanation)
STAL 20

//TESTING
	//ENUT 3
	//CALL EndingScene
	//ENDB

NoFade
ENDA

Turn4Reinforcements:
CAM1 [18,5]
STAL 20
LoadEnemies(Turn4ReinforcementsNEGroup)
STAL 10

CAM1 [7,26]
STAL 20
LoadEnemies(Turn4ReinforcementsSWGroup)
STAL 10

NoFade
ENDA

RimaJoins:

CHECK_ALIVE Bolton
IFEQ 1 sC s0
//If Bolton is dead, Rima enters the map through this turn event
	CAM1 [7,20]
	STAL 10
	LoadPlayerUnits(NewUnit_Rima)
	STAL 20
GOTO 2
LABEL 1
//If Bolton is alive, Rima enters the map by being selected through Early Leave
	LoadPlayerUnits(NewUnit_RimaOffscreen)
	SetUnitStatus(Rima,State,0x00000009)
LABEL 2
NoFade
ENDA

Turn7SalmanWakesUp:
CAM1 Salman
STAL 20
Text(HubC_3SalmanWakesUp)
NoFade
ENDA

Turn8Reinforcements:
CAM1 [18,22]
STAL 20
LoadEnemies(Turn8ReinforcementsSEGroup)
STAL 10
NoFade
ENDA

Turn9ReinforcementWarning:
CAM1 [18,22]
STAL 20
CURSOR_FLASHING [25,21]
STAL 30
REMOVE_CURSORS
Text(HubC_3MajorBossWarning)
STAL 10
NoFade
ENDA

Turn10Reinforcements:
CAM1 [18,22]
STAL 20
LoadEnemies(Turn10ReinforcementsSEGroup)
STAL 10
NoFade
ENDA


//----------------------------------------------
//EndingScene
//----------------------------------------------

EndingScene:
//Objective progress was tracked here
COUNTER_SET 0 0
COUNTER_SET 1 0

FADI 10

//If player wins before Turn 5, Rima joins automatically
CHECK_EXISTS Rima
IFEQ 11 sC s0
	LoadPlayerUnits(NewUnit_Rima)
LABEL 11

CLEA 0
CLEE 0
CLEN 0
LoadCutsceneUnits(EndingGroup1)
MUSC FinInTheCloudsID
CAM1 [7,20]
ClearBackground
STAL 60
MOVE 0 Runamel [4,20]
ENUN
Text(HubC_3Ending1)
STAL 20

LoadCutsceneUnits(EndingGroup2)
STAL 20
Text(HubC_3Ending2)
STAL 20

MOVE 0 Runamel [19,21]
STAL 10
MOVE 0 C3Pirate [19,20]
MOVE 0 C3Traitor [19,22]
ENUN
DISA Runamel
DISA C3Pirate
DISA C3Traitor
STAL 10
FADI 10
SetBackground(0x1D) //wilderness, close to mountains
MUSC StillZephyrsID
TEXTSTART
TEXTSHOW HubC_3Ending3
TEXTEND
FADI 10
REMA
STAL 20

CHECK_ALIVE Bolton
IFNE 1 sC s0
	CHECK_ALIVE Rima
	IFNE 3 sC s0
		SetBackground(0x19) //evening wilderness, nearby forest
		MUSC WelcomeAboardMateyID
		TEXTSTART
		TEXTSHOW HubC_3EndingBothRecruits
		TEXTEND
		FADI 10
		REMA
		STAL 20
	GOTO 4
	LABEL 3
		SetBackground(0x19) //evening wilderness, nearby forest
		MUSC WelcomeAboardMateyID
		TEXTSTART
		TEXTSHOW HubC_3EndingBoltonOnly
		TEXTEND
		FADI 10
		REMA
		STAL 20
	LABEL 4
GOTO 2
LABEL 1
	CHECK_ALIVE Rima
	IFNE 5 sC s0
		SetBackground(0x19) //evening wilderness, nearby forest
		MUSC WelcomeAboardMateyID
		TEXTSTART
		TEXTSHOW HubC_3EndingRimaOnly
		TEXTEND
		FADI 10
		REMA
		STAL 20
	LABEL 5
	//No conversation if both Bolton and Rima are dead
LABEL 2

MUSCSLOW 0x7FFF
SetBackground(0x17) //prison cell
STAL 10
TEXTSTART
TEXTSHOW HubC_3Ending4
TEXTEND
FADI 4
REMA
STAL 20

//GOTO_TITLE //Playtesting
GoToHubChapter(HubC)
NoFade
ENDA
